// GiveDirectly's cost effectiveness Analysis. Taken from GiveWell

import common(discount_rate, pv, years)
// units
unit usd usddonated usdtransfered usdinvested usdtransfered usdconsumed usdreturned
unit people value households
unit discountedpv

// Donation size is arbitrary
donation_size = 100000 usddonated

transfers_of_cost = 0.83 usdtransfered usddonated-1
total_funding_available = transfers_of_cost * donation_size
total_transfer_size = 1000 usdtransfered households-1
average_household_size = 4.7 people households-1
size_of_transfer_per_people = total_transfer_size / average_household_size

percent_of_transfers_invested = 0.39 usdinvested usdtransfered-1
amount_invested = percent_of_transfers_invested * size_of_transfer_per_people
duration_of_initial_consumption = 1 years

// This is really awkwardly hacked into types... I wonder if there's a better
// way to do this
percent_of_transfers_consumed = 
 1 usdconsumed usdtransfered-1 - 
 percent_of_transfers_invested * 
 (1 usdconsumed usdinvested-1)

consumption_possible_by_funds = 
  percent_of_transfers_consumed * 
  size_of_transfer_per_people / 
  duration_of_initial_consumption
return_on_investment = 0.10 usdreturned usdinvested-1 years-1
usd_returned_consumed = 1 usdconsumed usdreturned-1
annual_increase_consumption_by_roi = return_on_investment * amount_invested * usd_returned_consumed

log_consumption_created baseline increase = ln( (baseline + increase) / baseline)
baseline_consumption_per_capita = 285.92 usdconsumed people-1 years-1
immediate_increase_of_consumption = 
  log_consumption_created baseline_consumption_per_capita consumption_possible_by_funds *
  duration_of_initial_consumption
future_increase_in_ln_consumption =  
  log_consumption_created baseline_consumption_per_capita annual_increase_consumption_by_roi
duration_of_investment = 10 years
pv_of_investment = pv future_increase_in_ln_consumption discount_rate (duration_of_investment - (1 years))
percent_of_investment_returned = 0.2 usdreturned usdinvested-1
duration_of_end_of_benefits = 1 years
percent_roi_final_year = (percent_of_investment_returned / duration_of_end_of_benefits) + return_on_investment
final_year_discount = discount_rate ^ duration_of_investment
pv_of_last_years = (log_consumption_created baseline_consumption_per_capita (amount_invested * percent_roi_final_year * usd_returned_consumed)) / final_year_discount * duration_of_end_of_benefits
total_pv_increase = pv_of_last_years + pv_of_investment + immediate_increase_of_consumption
discount_for_potential_negative_spillover = 0.95
total_pv_after_spillover = discount_for_potential_negative_spillover * total_pv_increase
total_increase_ln_consumption_per_household = total_pv_after_spillover * average_household_size

number_of_transfers_made = total_funding_available / total_transfer_size
total_units_of_ln_consumption = number_of_transfers_made * total_increase_ln_consumption_per_household
value_of_ln_consumption = 1.44 value people-1 years-1
total_units_of_value = value_of_ln_consumption * total_units_of_ln_consumption

percent_of_cash_benefits_from_short_term = immediate_increase_of_consumption / total_pv_increase
percent_of_cash_benefits_from_long_term = (pv_of_last_years + pv_of_investment)  / total_pv_increase

// Downside Adjustments
risk_of_cash_not_reach_participants = 0.03

risk_of_within_org_fungibility = 0.05

downside_adjustment_factor = 1 - risk_of_within_org_fungibility - risk_of_cash_not_reach_participants
value_after_downsides = downside_adjustment_factor * total_units_of_value

// Downside effects
changes_in_ppp = -0.07
developmental_effects = 0.05
reduced_morbidity = 0.04
child_mortality_effects = 0.04

total_adjustment_factor = 1 + changes_in_ppp + developmental_effects + reduced_morbidity + child_mortality_effects

// After adjustments
total_value_after_adjustments = total_units_of_value * total_adjustment_factor * downside_adjustment_factor
total_value_after_adjustments_per_dollar = total_value_after_adjustments / donation_size
